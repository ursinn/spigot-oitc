plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group = 'dev.ursinn.spigot'
version = '1.6-SNAPSHOT'
description = 'OITC'

repositories {
    mavenCentral()
    maven {
        name = 'codemc-nms-repo'
        url = 'https://repo.codemc.org/repository/nms/'
    }
    maven {
        name = 'ossrh'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.20-R0.1-SNAPSHOT'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Javadoc
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// Publishing Stack - Start
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'oitc'
            from components.java
            pom {
                packaging = 'jar'
                name = 'OITC'
                description = 'One in the Camber'
                url = 'https://github.com/ursinn/spigot-oitc'
                inceptionYear = '2022'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ursinn/spigot-oitc/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ursinn'
                        name = 'Ursin Filli'
                        email = 'mail@ursinn.dev'
                        timezone = '+1'
                        url = 'https://ursinn.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ursinn/spigot-oitc.git'
                    developerConnection = 'scm:git:git@github.com:ursinn/spigot-oitc.git'
                    url = 'https://github.com/ursinn/spigot-oitc'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/ursinn/spigot-oitc/issues'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhS01Username
                password ossrhS01Password
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

// Publishing Stack - End